01 suma srazek mesic 1 = 

-- takto NE
VAR _varianta1 =
 CALCULATE(
    SUM(data[Srazky]),
    FILTER(
        -- kopie tabulky dim_datumy se všemi jejími filtry
        dim_datumy,
        dim_datumy[mesic] = 1))

--- takto ano, pokud nefunguje varianta3
VAR _varianta2 = 
    CALCULATE(
        SUM(data[Srazky]),
        FILTER(
            -- kopie jediného sloupce tabulky dim_datumy, kdy
            -- filtry nad ostatními sloupci (tj. rok) zůstávají
            ALL(dim_datumy[mesic]),
        dim_datumy[mesic] = 1))

-- takto ano, tzv. booleanovský zápis, nejrychlejší způsob výpočtu, 
-- který ale nefunguje vždy, proto _varianta2
VAR _varianta3 = 
    CALCULATE(
        SUM(data[Srazky]),
    -- nahrazujeme filtr nad sloupcem [mesic], ostatní filtry tabulky
    -- dim_datumy zůstávají
    dim_datumy[mesic] = 1)


RETURN _varianta3
    
____________________________________________________________________________


02 suma srazek vse = 
-- takto ne
VAR _varianta1 = 
CALCULATE(
    SUM(data[Srazky]),
    ALL(data))

VAR _varianta2 = 

CALCULATE(
    SUM(data[Srazky]),
    ALL(dim_datumy[rok])
    ,ALL(dim_lokality[Zeme])
)

RETURN
_varianta2


______________________________________________________________________________


15 nejdestivejsi rok mesice = 
IF(
-- suma srazek v danem roce, mesici
SUM(data[Srazky])
=
-- maximum ze sum srazek v danem mesici
    MAXX(
    ALL(dim_datumy[rok]),
    CALCULATE(SUM(data[Srazky])))
    ,"GOLD")

_______________________________________________________________________________

16 top3 nejdestivejsi mesice = 
VAR _tabulka = 
    ADDCOLUMNS(
    ALL(dim_datumy[rok]),
    "suma srazek",
    CALCULATE(SUM(data[Srazky])))

VAR _tabulka_rank  =
FILTER(
ADDCOLUMNS(
    _tabulka,
    "rank srazek",
    RANKX(_tabulka,[suma srazek],,DESC)),
[rank srazek] <=3)

RETURN
IF(SUM(data[Srazky]) >= MINX(_tabulka_rank,[suma srazek]),"CYAN")

__________________________________________________________________________________

17 top 10 nejvyssich hodnot = 
VAR _tabulka = 
    ADDCOLUMNS(
    ALL(dim_datumy[rok],dim_datumy[mesic]),
    "suma srazek",
    CALCULATE(SUM(data[Srazky])))
VAR _tabulka_rank  =
FILTER(
ADDCOLUMNS(
    _tabulka,
    "rank srazek",
    RANKX(_tabulka,[suma srazek],,DESC)),
[rank srazek] <=10)
RETURN
IF(SUM(data[Srazky]) >= MINX(_tabulka_rank,[suma srazek]),"CYAN")

_____________________________________________________________________


cv11 rok max srazek ruzyne = 
VAR _tabulka = 
    ADDCOLUMNS(
    ALL(dim_datumy[rok]),
    "suma srazek",
    CALCULATE(
        SUM(data[Srazky]),dim_lokality[Lokalita] = "RUZYNE"))
VAR _max_srazky = MAXX(_tabulka,[suma srazek])
RETURN
CONCATENATEX(
    FILTER(
        _tabulka,
        [suma srazek] = _max_srazky),
dim_datumy[rok],", ")
__________________________________________________


cv07 minimalni teplota = CALCULATE(MIN(data[Teplota]),ALL(dim_lokality[Lokalita]))
